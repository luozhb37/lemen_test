*** Settings ***
Suite Setup       Run keywords    系统参数设置-发票核销模式
...               AND    登录
...               AND    新增海运出口业务
Suite Teardown    close all browsers
Test Setup        Reload Page
Resource          ../../../../../Resources/M3基础操作/M3--引用.txt

*** Test Cases ***
对冲核销2--按发票(收支相等)
    点击业务管理
    点击业务台帐
    海运出口列表查询业务编号    ${BUSINESSNO-SUIT-SEAEXPORT}
    进入台帐详情    ${BUSINESSNO-SUIT-SEAEXPORT}
    从台帐详情进入费用页面
    ${recrow}    新增收入费用-选定费用    2000
    ${payrow}    新增支出费用-选定费用    2000
    点击对账-费用信息页面
    选择对冲费用-生成对账单（费用信息入口）
    ${check_no_rec}    生成对账单
    点击业务管理
    点击结算管理
    点击发票
    点击新增发票
    查询-新增发票（按对账单）    ${check_no_rec}    ${BUSINESSNO-SUIT-SEAEXPORT}
    全选记录-列表页面
    ${invoiceno}    汇总开票-按对账单
    点击对冲
    点击对冲申请
    查询-对冲申请（按发票）    ${invoiceno}
    全选记录-列表页面
    点击核销-对冲申请（按发票、对账单、账单）
    点击对冲申请-（核销-页面选择）
    ${duibillno}    ${duicong_billamount}    ${recbillno}    对冲核销--按发票、对账单、账单（收入>=支出）
    点击对冲单管理
    查询-对冲单管理（默认是已核销）    ${duibillno}
    ${page}    获取列表记录数
    应该包含    ${page}    共1条
    点击收款
    点击收款单管理
    查询-收款单管理（已核销）    ${recbillno}
    ${reccode}    获取对冲金额-收款单管理已核销列表
    应该要相等    ${reccode}    ${duicong_billamount}
    点击结算管理
    #费用列表查看收款单号及实付金额
    ${rec_paybillno}    ${amount_rec}    费用列表获取收款单号-选定费用    ${BUSINESSNO-SUIT-SEAEXPORT}    ${recrow}
    ${fee_paybillno}    ${amount_pay}    费用列表获取付款单号-选定费用    ${BUSINESSNO-SUIT-SEAEXPORT}    ${payrow}
    应该要相等    ${rec_paybillno}    ${duibillno}
    应该要相等    ${fee_paybillno}    ${duibillno}
    应该要相等    2,000.00    ${amount_rec}
    应该要相等    2,000.00    ${amount_pay}

对冲核销2--按费用明细(收入大)
    点击业务管理
    点击业务台帐
    海运出口列表查询业务编号    ${BUSINESSNO-SUIT-SEAEXPORT}
    进入台帐详情    ${BUSINESSNO-SUIT-SEAEXPORT}
    从台帐详情进入费用页面
    ${recrow}    新增收入费用-选定费用    3000
    点击对账-费用信息页面
    选择收入（支出）费用-生成对账单（费用信息入口）    rec
    ${check_rec}    生成对账单
    点击业务管理
    点击结算管理
    点击发票
    查询-新增发票（按对账单）    ${check_rec}    ${BUSINESSNO-SUIT-SEAEXPORT}
    全选记录-列表页面
    ${invoiceno_rec}    汇总开票-按对账单
    #
    点击结算管理
    点击业务管理
    点击业务台帐
    海运出口列表查询业务编号    ${BUSINESSNO-SUIT-SEAEXPORT}
    进入台帐详情    ${BUSINESSNO-SUIT-SEAEXPORT}
    从台帐详情进入费用页面
    ${payrow}    新增支出费用-选定费用    2000
    点击对账-费用信息页面
    选择收入（支出）费用-生成对账单（费用信息入口）    pay
    ${check_pay}    生成对账单
    点击业务管理
    点击结算管理
    点击发票
    查询-新增发票（按对账单）    ${check_pay}    ${BUSINESSNO-SUIT-SEAEXPORT}
    全选记录-列表页面
    点击开票-新增发票
    ${invoiceno}    按对账单-应付登记发票
    点击对冲
    点击对冲申请
    查询-对冲申请（按费用明细）    \    ${BUSINESSNO-SUIT-SEAEXPORT}
    全选记录-列表页面
    点击选择-对冲申请列表（按费用明细）
    点击对冲核销-对冲申请列表（按费用明细）
    ${duibillno}    ${duicong_billamount}    ${recbillno}    对冲核销--按费用明细（收入>=支出）
    点击对冲单管理
    查询-对冲单管理（默认是已核销）    ${duibillno}
    ${page}    获取列表记录数
    应该包含    ${page}    共1条
    点击收款
    点击收款单管理
    查询-收款单管理（已核销）    ${recbillno}
    ${reccode}    获取对冲金额-收款单管理已核销列表
    应该要相等    ${reccode}    ${duicong_billamount}
    点击结算管理
    #费用列表查看收款单号及实付金额
    ${rec_paybillno}    ${amount1}    费用列表获取收款单号-选定费用    ${BUSINESSNO-SUIT-SEAEXPORT}    ${recrow}
    ${fee_paybillno}    ${amount}    费用列表获取付款单号-选定费用    ${BUSINESSNO-SUIT-SEAEXPORT}    ${payrow}
    应该要相等    ${fee_paybillno}    ${duibillno}
    应该要相等    ${rec_paybillno}    ${duibillno}
    应该要相等    2,000.00    ${amount}
    应该要相等    3,000.00    ${amount1}
